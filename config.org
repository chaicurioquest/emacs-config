#+TITLE: Emacs Literate Config
#+TODO: ACTIVE | CANCELLED
#+STARTUP: indent
#+DATE: \today
#+FILETAGS: ::
#+OPTIONS: toc:t num:nil
#+PROPERTY: header-args :eval never-export
#+CREATED: 2025-07-12
#+LAST_MODIFIED: [2025-08-12 Tue 16:06]

* Purpose
This Org-mode configuration is the single source for my Emacs 30 setup, designed to be:
- **Future-proof** · Structured with comments, tables, CANCELLED blocks.
- **Reliable**     · Reproducible packages via straight.el, lazy loading.
- **Minimal**      · Single config.org, minimal hooks/keybindings.
- **Portable**     · Device-specific settings via device.el.
- **Synced**       · GitHub + Syncthing.
- **Modular**      · All extensions live in org/*.org files.

* My Digital Workflow :workflow:
| Purpose                         | Solution                              | Notes (device-specific in brackets)                              | Keybindings                          |
|---------------------------------|---------------------------------------|------------------------------------------------------------------|--------------------------------------|
| Package management              | use-package + straight.el             | Lazy loading, reproducible installs (all devices)                | —                                    |
| Filter Org headings             | sparse trees                          | Built-in Org (all devices)                                       | C-c /                                |
| Focus on subtree                | org-tree-to-indirect-buffer           | Built-in Org (all devices)                                       | C-c C-x b                            |
| Completion & search             | vertico + orderless                   | Fast, fuzzy, vertical UI (all devices)                           | —                                    |
| Buffer switching                | ibuffer                               | Better than default list (all devices)                           | C-x C-b                              |
| Insert timestamps               | custom functions                      | Various formats (all devices)                                    | C-c i t/d/o                          |
| Export to PDF                   | Org → LaTeX (AUCTeX)                  | pdf-tools for viewing (laptop preferred)                         | C-c C-e l p                          |
| Encrypt sections                | org-crypt                             | GPG-encrypted headings (all devices)                             | —                                    |
| Snippet management              | yankpad + yasnippet                   | Org-style snippets (all devices)                                 | C-c y / C-c Y                        |
| Spell checking                  | flyspell + Hunspell                   | UK English dictionary (all devices)                              | M-$                                  |
| PDF viewing & annotation        | pdf-tools + org-noter                 | Highlighting & synced notes (laptop preferred)                   | M-x org-noter                        |
| PDF side-by-side preview        | my/org-pdf-vertical-view              | Quick reference while writing (all devices)                      | C-c v                                |
| Zettelkasten / note-taking      | org-roam                              | Dailies, backlinks, UI on laptop                                 | C-c r n/f/i/d/T/P/N/D/t/r/s/g        |
| Bibliography & citations        | citar + org-roam integration          | Zotero sync, literature notes in roam (all devices)              | C-c i c/n/N/o/O                      |
| File-level tags                 | filetags                              | Vertico-powered tag completion (all devices)                     | C-c f t                              |
| Config tangling                 | org-babel-tangle + custom helpers     | Single or all files (all devices)                                | C-c t / C-c T                        |
| Quick capture                   | org-capture                           | Templates for notes/tasks (all devices)                          | C-c c                                |
| Toggle list items               | org-toggle-item                       | Convert text ↔ list (all devices)                                | C-c i i                              |
| Window navigation               | windmove + winner-mode + ace-window   | Move, undo/redo, jump (all devices)                              | Shift+Arrow / M-o                    |
| Execute code blocks             | Org Babel                             | Run shell/LaTeX/Python inline (all devices)                      | C-c C-c                              |
| Git sync for notes              | Magit                                 | Pull/push repo changes (all devices)                             | C-c g p / u                          |

* ACTIVE Core Setup

** Debug startup
#+BEGIN_SRC emacs-lisp
(when (getenv "MY_DEBUG_DEVICE")
  (message "=== STARTING CONFIG ==="))
#+END_SRC

** Garbage collection
#+BEGIN_SRC emacs-lisp
(add-hook 'emacs-startup-hook
          (lambda () (setq gc-cons-threshold (* 2 1000 1000))))
#+END_SRC

** Disable startup screen.
#+BEGIN_SRC emacs-lisp
(setq inhibit-startup-screen t)
#+END_SRC

** Load device detection (with fallback)
#+BEGIN_SRC emacs-lisp
(condition-case err
    (load (expand-file-name "device.el" user-emacs-directory))
  (error (message "Failed to load device.el: %S" err)))

;; Fallback if device.el missing or failed
(unless (boundp 'my-device)
  (defvar my-device 'laptop)
  (message "my-device not set → defaulting to 'laptop"))

(message "Device: %s" my-device)
#+END_SRC

** Set default directory per device.
#+BEGIN_SRC emacs-lisp
(setq default-directory
      (cond ((eq my-device 'laptop) (expand-file-name "org/" "/wspace/"))
            ((eq my-device 'tablet) (expand-file-name "org/" (getenv "HOME")))
            ((eq my-device 'phone)  (expand-file-name "storage/notes" (getenv "HOME")))
            (t (expand-file-name "~/"))))

(unless (file-directory-p default-directory)
  (make-directory default-directory t))
(message "Default directory: %s" default-directory)
#+END_SRC

** Global paths — defined very early

#+BEGIN_SRC emacs-lisp
;; Define paths early for global reuse (absolute/resolved for reliability)
(defvar bib-path (expand-file-name "bib/references.bib" default-directory))
(defvar my-citar-library-paths '("/wspace/src/zotero-kbase/storage"))
(defvar setupfile (expand-file-name "latex/setup-latex.org" default-directory))
(defvar org-roam-directory (expand-file-name "roam" default-directory))
(defvar org-roam-dailies-directory (expand-file-name "daily" org-roam-directory))
(defvar my-calibre-library-dir "/wspace/src/calibre-ebooks")
  
;; Create roam directories if missing
(dolist (d (list org-roam-directory org-roam-dailies-directory))
  (unless (file-directory-p d) (make-directory d t)))

(prefer-coding-system 'utf-8)
(setq default-buffer-file-coding-system 'utf-8)
#+END_SRC

** Tangling utilities
#+BEGIN_SRC emacs-lisp
;; modular org files tangle process.Tangling only happens when the .org file is newer than the .el file. 
(defun my-tangle-config-org ()
  "Tangle only config.org → config.el (C-c t)."
  (interactive)
  (condition-case err
      (progn
        (org-babel-tangle-file (expand-file-name "config.org" user-emacs-directory))
        (message "config.org tangled"))
    (error (message "Failed to tangle config.org: %S" err))))

(defun my/tangle-if-needed (org-file el-file)
  "Tangle ORG-FILE to EL-FILE only if ORG-FILE is newer."
  (let ((org-full (expand-file-name org-file user-emacs-directory))
        (el-full  (expand-file-name el-file  user-emacs-directory)))
    (when (or (not (file-exists-p el-full))
              (file-newer-than-file-p org-full el-full))
      (require 'org)
      (org-babel-tangle-file org-full el-full "emacs-lisp")
      (message "Tangled: %s → %s" org-file el-file))))
#+END_SRC

** Tag-based refiling
#+BEGIN_SRC emacs-lisp
;; automatically refiling (moving) Org headings based on their tags added on 2025-09-20
(defcustom my-org-refile-to-ids nil
  "Tag to target ID mappings."
  :group 'sacha
  :type '(repeat (cons string string)))
#+END_SRC

** Smart org-agenda-files (top-level + roam/)
#+BEGIN_SRC emacs-lisp
(defun my/update-agenda-files ()
  "Set `org-agenda-files` to top-level .org files in `default-directory`,
plus recursive .org files from the 'roam' subdirectory, excluding unwanted dirs."
  (interactive)
  (let* ((notes-dir (file-name-as-directory default-directory))
         (roam-dir (expand-file-name "roam/" notes-dir))
         (excluded-dirs '("build" "ltximg" "images" ".attach" ".autosaves" ".backups" "bib" "latex" ".git"))
         (dir-predicate (lambda (d)
                          (let ((dirname (file-name-nondirectory d)))
                            (not (or (string-prefix-p "." dirname)
                                     (member dirname excluded-dirs))))))
         ;; Top-level .org files in root
         (top-level-files (when (file-directory-p notes-dir)
                            (directory-files notes-dir t "\\.org\\'")))
         (top-level-orgs (seq-filter (lambda (f)
                                       (and (file-regular-p f)
                                            (not (string-match-p "/\\.#" f))))
                                     top-level-files))
         ;; Recursive .org files in roam
         (roam-files (when (file-directory-p roam-dir)
                       (directory-files-recursively roam-dir "\\.org\\'" nil dir-predicate)))
         (roam-orgs (seq-filter (lambda (f)
                                  (and (file-regular-p f)
                                       (not (string-match-p "/\\.#" f))))
                                roam-files))
         (all-orgs (append top-level-orgs roam-orgs)))
    (if (not all-orgs)
        (progn
          (setq org-agenda-files nil)
          (message "my/update-agenda-files: no valid .org files found in %s or roam/" notes-dir))
      (setq org-agenda-files all-orgs)
      (message "org-agenda-files set (%d files) from %s and roam/" (length org-agenda-files) notes-dir))))

;; Initialize once
(my/update-agenda-files)

;; Rescan only when saving an Org file under the notes dir (including subdirs like roam)
(add-hook 'after-save-hook
          (lambda ()
            (when (and (derived-mode-p 'org-mode)
                       buffer-file-name
                       (string-prefix-p (file-name-as-directory default-directory)
                                        (file-name-directory (file-truename buffer-file-name))))
              (my/update-agenda-files))))
#+END_SRC


** Startup finished message
#+BEGIN_SRC emacs-lisp
(when (getenv "MY_DEBUG_DEVICE")
  (message "=== STARTUP COMPLETE ==="))
#+END_SRC

* ACTIVE Package Management
Configure package managers and lightweight, universal packages with lazy loading.
Heavy or laptop-specific packages are in org/workflows.org; org-roam and bibliographic tools in org/roam.org; filetags in org/filetags.org.

| Package        | Purpose (device-specific in brackets)                              | Keybindings                  | Loading Trigger |
|----------------|--------------------------------------------------------------------|------------------------------|-----------------|
| org            | Core Org-mode (built-in, essential)                                | Org-mode keys                | Built-in        |
| org-roam       | Zettelkasten note-taking (graph UI on laptop only)                 | C-c r …                      | Startup         |
| citar          | Bibliography interface + Zotero integration (all devices)          | C-c i c/n/N/o/O              | On demand       |
| pdf-tools      | In-buffer PDF viewing & highlighting (laptop preferred)            | —                            | On demand       |
| org-noter      | Synced PDF annotations in Org (laptop preferred)                   | M-x org-noter                | On demand       |
| yasnippet      | Snippet engine (powers yankpad, all devices)                       | TAB, C-c s                   | On demand       |
| yankpad        | Org-style snippet library (all devices)                            | C-c y / C-c Y                | On demand       |
| vertico        | Vertical completion UI (all devices)                               | —                            | Startup         |
| orderless      | Fuzzy matching for completion (all devices)                        | —                            | Startup         |
| marginalia     | Rich minibuffer annotations (all devices)                          | —                            | Startup         |
| embark         | Contextual actions (all devices)                                   | C-. / C-;                    | Startup         |
| consult        | Enhanced search & navigation (all devices)                         | C-s, C-c h/k/b/p             | On demand       |
| ibuffer        | Better buffer list (all devices)                                   | C-x C-b                      | C-x C-b         |
| org-crypt      | Section encryption (all devices)                                   | —                            | org-mode hook   |
| cdlatex        | Fast math input in Org (all devices)                               | TAB (contextual)             | org-mode hook   |
| auctex         | LaTeX editing & export (all devices)                               | —                            | On demand       |
| flyspell       | Spell checking with Hunspell (all devices)                         | M-$                          | On demand       |
| magit          | Git interface for notes repo (all devices)                         | C-x g, C-c g p/u             | On demand       |
| ace-window     | Fast window switching (all devices)                                | M-o                          | On demand       |

** Org Base Configuration
#+BEGIN_SRC emacs-lisp
(eval-when-compile (require 'use-package))
(use-package org
  :straight (:type built-in)
  :ensure nil
  :config
  ;; Core requires
  (require 'oc)
  (require 'oc-biblatex)
  ;; Enable refiling to any heading in agenda files (incl. roam/notes/mobile)
  (setq org-refile-targets '((org-agenda-files :maxlevel . 5)))
  (setq org-refile-use-outline-path 'file) ;; Show file paths in prompt
  (setq org-outline-path-complete-in-steps nil) ;; Complete in one step (Vertico fuzzy)
  (setq org-refile-allow-creating-parent-nodes 'confirm)
  (setq org-refile-use-cache t))  ;; Cache targets for speed
#+END_SRC

** Ensure use-package is available.
#+BEGIN_SRC emacs-lisp
(eval-when-compile
  (require 'use-package))
#+END_SRC

** File manipulation library (loaded on demand).
#+BEGIN_SRC emacs-lisp
(use-package f
  :straight t
  :defer t)
#+END_SRC

** Hash table utilities (loaded on demand).
#+BEGIN_SRC emacs-lisp
(use-package ht
  :straight t
  :defer t)
#+END_SRC

** Ibuffer for buffer management (loaded on C-x C-b).
#+BEGIN_SRC emacs-lisp
(use-package ibuffer
  :straight t
  :defer t
  :bind ("C-x C-b" . ibuffer))
#+END_SRC

** Encrypt org files
#+BEGIN_SRC emacs-lisp
(use-package org-crypt
  :ensure nil                        ;; Do not install from ELPA
  :straight nil                      ;; Do not use straight.el
  :defer t                           ;; Load when needed (on demand)
  :config
  (setq org-crypt-use-before-save nil) ;; Optional: prevent auto-encryption on save
  (require 'org-crypt))
#+END_SRC

** Flyspell for spell checking (loaded on M-$).
#+BEGIN_SRC emacs-lisp
(use-package flyspell
  :defer t
  :bind ("M-$" . flyspell-correct-word-before-point)
  :init
  ;; Force Hunspell with UK English dictionary
  (setq ispell-program-name "hunspell"
        ispell-dictionary "en_GB"
        ispell-local-dictionary "en_GB"
        ispell-local-dictionary-alist
        '(("en_GB" "[[:alpha:]]" "[^[:alpha:]]" "[']" t
           ("-d" "en_GB") nil utf-8)))
  :config
  ;; Enable flyspell everywhere you write text
  (add-hook 'text-mode-hook #'flyspell-mode)
  ;; For code: only check strings & comments
  (add-hook 'prog-mode-hook #'flyspell-prog-mode)

  ;; Correction interface for M-$
  (use-package flyspell-correct :straight t))

#+END_SRC

** CD Latex package for latex equations
#+BEGIN_SRC emacs-lisp
 (use-package cdlatex 
  :straight t 
  :defer t
  :hook (org-mode . org-cdlatex-mode))
#+END_SRC
   
** AUCTeX is an extensible package for writing and formatting TeX files in Emacs and XEmacs
#+BEGIN_SRC emacs-lisp
(use-package auctex
  :straight t
  :defer t  ;; Load on demand
  :hook (LaTeX-mode . (lambda () (turn-on-reftex) (flyspell-mode)))  ;; Optional: RefTeX for refs, spell-check
  :config
  (setq TeX-auto-save t
        TeX-parse-self t))
#+END_SRC

** Yasnippet package for adding snippets in org files
#+BEGIN_SRC emacs-lisp
(use-package yasnippet
  :straight t
  :bind ("C-c s" . yas-insert-snippet)  ;; Pop up selectable snippets (e.g., tbl, fig)
  :config
  (yas-global-mode 1)
  (setq yas-indent-line 'fixed)  ;; Preserves indentation
  (add-to-list 'yas-snippet-dirs (expand-file-name "snippets" user-emacs-directory))
  (yas-reload-all)
  (add-hook 'org-mode-hook #'yas-minor-mode)
  :diminish yas-minor-mode)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package org-download
  :straight t
  :after org
  :bind ("C-c C-x C-s" . org-download-screenshot)  ;; Capture screen to Org
  :config
  (setq org-download-method 'attach  ;; Use org-attach dir
        org-download-image-dir ".attach/images"  ;; Subdir for organization
        org-download-screenshot-method "scrot -s %s")  ;; Matches your mathpix
  (add-hook 'org-mode-hook #'org-download-enable))
#+END_SRC

** Yankpad: Org-mode snippet library on top of Yasnippet
#+BEGIN_SRC emacs-lisp
(use-package yankpad
  :straight t
  :bind
  (("C-c Y" . yankpad-expand)  ;; Expand snippet at point with yasnippet evaluation
   ("C-c y" . yankpad-insert)) ;; Insert snippet as plain text (no evaluation)
  :config
  ;; Set yankpad file in a portable location
  (setq yankpad-file (expand-file-name "org/yankpad.org"
                                       user-emacs-directory))
  ;; Ensure directory exists
  (let ((yankpad-dir (file-name-directory yankpad-file)))
    (unless (file-directory-p yankpad-dir)
      (make-directory yankpad-dir t)))

  ;; Load snippets immediately
  (yankpad-reload)

  ;; Optional: auto-reload in Org buffers
  (add-hook 'org-mode-hook #'yankpad-reload))
#+END_SRC

** Vertico: vertical completion UI Work well with org-roam (and Emacs in general) much faster, more flexible, and user-friendly. 
#+BEGIN_SRC emacs-lisp
(use-package vertico
  :straight t
  :defer t
  :init
  (vertico-mode))
#+END_SRC

** Orderless: smart fuzzy matching for completion
#+BEGIN_SRC emacs-lisp
(use-package orderless
  :straight t
  :defer t
  :custom
  (completion-styles '(orderless))
  (completion-category-defaults nil)
  (completion-category-overrides '((file (styles partial-completion)))))
#+END_SRC

** Future enhancement Consult package: enhanced commands. :future:
Powerful, fast,and flexible search/navigation UI (search, buffer switch, etc.) for working with Org-roam and Emacs in general.It can be enabled later for future optimization.

#+BEGIN_SRC emacs-lisp 
(use-package consult
  :straight t
  :bind
  (("C-s" . consult-line)
  ("C-c h" . consult-org-heading)
  ("C-c k" . consult-ripgrep)
  ("C-c b" . consult-buffer)
  ("C-c p" . consult-find)))
#+END_SRC

** bibtex-completion for bibtex citar implementation
#+BEGIN_SRC emacs-lisp
;; -----------------------------
;; Marginalia — Rich Annotations (metadata for minibuffer)
;; -----------------------------
(use-package marginalia
  :straight t
  :init (marginalia-mode)
  :config
  (setq marginalia-align 'right)
  (message "✅ Marginalia annotations active."))
#+END_SRC

#+BEGIN_SRC emacs-lisp
;; -----------------------------
;; Embark — Contextual Actions
;; -----------------------------
(use-package embark
  :straight t
  :bind (("C-." . embark-act) ;; main action key
         ("C-;" . embark-dwim)) ;; smart default action
  :config
  (setq prefix-help-command #'embark-prefix-help-command)
  (message "✅ Embark contextual actions active."))
#+END_SRC

#+BEGIN_SRC emacs-lisp
;; Integrate Embark + Consult (if you have consult; otherwise omit)
(use-package embark-consult
  :straight t
  :after (embark consult)
  :hook (embark-collect-mode . consult-preview-at-point-mode)
  :config (message "✅ Embark-Consult integration enabled."))
#+END_SRC

** RefTeX for Advanced Citation/Ref Handling.
If using AUCTeX for LaTeX exports, add reftex package.

#+BEGIN_SRC emacs-lisp
(use-package reftex
  :straight t
  :defer t
  :diminish reftex-mode)
#+END_SRC

** pdf tools: Enable in-buffer PDF viewing in Emacs (rather than opening PDFs in external viewers)

#+BEGIN_SRC emacs-lisp
;; Use features like highlighting, annotations, or text selection. Work along with org-noter for taking notes synchronized with PDF pages
;; Updated on 2025-11-26 to fix epdfinfo executable error after Poppler upgrade.
;; ──────────────────────────────────────────────────────────────
;; PDF Tools + Org-Noter-PDFTools – FINAL WORKING VERSION (2025-11-26)
;; ──────────────────────────────────────────────────────────────
(use-package pdf-tools
  :straight (:host github :repo "vedang/pdf-tools" :build t)
  :defer t
  :commands (pdf-view-mode pdf-tools-install)
  :mode ("\\.pdf\\'" . pdf-view-mode)
  :magic ("%PDF" . pdf-view-mode)
  :init
  ;; No early loader
  (unless (memq my-device '(phone tablet))
    nil)
  :config
  ;; Set path and build if missing
  (setq pdf-info-epdfinfo-program
        (expand-file-name "straight/build/pdf-tools/epdfinfo" user-emacs-directory))
  (unless (file-executable-p pdf-info-epdfinfo-program)
    (pdf-tools-install))
  ;; Settings
  (setq pdf-view-display-size 'fit-page
        pdf-view-continuous t
        pdf-annot-activate-created-annotations t
        pdf-tools-handle-upgrades nil
        pdf-view-use-scaling t)  ; For HiDPI
  (add-to-list 'warning-suppress-types '(pdf-tools))
  (advice-add 'pdf-view-mode :around
              (lambda (orig-fun &rest args)
                (ignore-errors (apply orig-fun args))))
  (add-hook 'pdf-view-mode-hook
            (lambda ()
              (when buffer-file-name
                (pdf-cache-clear-data buffer-file-name))))
  (message "PDF-Tools ready"))
#+END_SRC


** pdf annotation org noter optional dependancies
#+BEGIN_SRC emacs-lisp
;; Optional for org-noter EPUB/DJVU (suppress warnings if not used)
(use-package nov
  :straight t
  :defer t)

(use-package djvu
  :straight t
  :defer t)
#+END_SRC

** Zotero/Bibliography Integration
#+BEGIN_SRC emacs-lisp
;; -----------------------------
;; Citar Package Configuration
;; -----------------------------
(use-package citar
  :straight t
  :no-require t
  :commands (citar-insert-citation citar-create-note)
  :custom
  (org-cite-global-bibliography (list bib-path))
  (citar-bibliography org-cite-global-bibliography)
  (citar-library-paths my-citar-library-paths)
  (citar-notes-paths (list (expand-file-name "literature" org-roam-directory)))
  (citar-file-note-extensions '("org"))
  (org-cite-insert-processor 'citar)
  (org-cite-follow-processor 'citar)
  (org-cite-activate-processor 'citar)
  (citar-file-open-function 'find-file)
  :hook
  ((org-mode . citar-capf-setup)
   (LaTeX-mode . citar-capf-setup))
  :config
  (require 'citar)  ; Load full package
  (message "✅ Citar ready."))

;; Install C-c i c in org buffers only (safe)
(add-hook 'org-mode-hook
          (lambda ()
            ;; prefer local-set-key to avoid overriding global maps
            (local-set-key (kbd "C-c i c") #'citar-insert-citation)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
;; Helper function to insert or create citation notes
(defun my/citar-insert-or-create-note ()
  "Insert citation if in Org-mode; otherwise, create bib note."
  (interactive)
  (if (derived-mode-p 'org-mode)
      (call-interactively 'citar-insert-citation)
    (call-interactively 'citar-create-note)))

;; Rebind your key (override the old one)
;;(global-set-key (kbd "C-c i c") 'my/citar-insert-or-create-note)
#+END_SRC

** Org noter + Citar annotation: added 2025-11-02
#+BEGIN_SRC emacs-lisp
(use-package org-noter
  :straight t
  :defer t
  :after org
  :commands org-noter
  :config
  (setq org-noter-notes-search-path (list (expand-file-name "literature" org-roam-directory)))
  (setq org-noter-auto-save-last-location t
        org-noter-always-create-frame nil
        org-noter-highlight-selected-text t
        org-noter-insert-note-no-questions t)
  (setq org-noter-hide-other nil)
  (message "✅ Org-noter ready for PDF annotations."))
#+END_SRC

** pdf annotation using org noter
#+BEGIN_SRC emacs-lisp
(use-package org-noter-pdftools
  :straight t
  :defer t
  :after (org-noter pdf-tools)
  :commands org-noter-pdftools-jump-to-note
  :config
  (defun my/org-noter-pdftools--convert-location (location)
    "Convert new org-noter-pdftools location struct to old (page . top) format."
    (if (consp location)
        location
      (cons (org-noter-pdftools--location-page location)
            (org-noter-pdftools--location-height location))))
  (add-to-list 'org-noter-notes-window-behavior '(org-noter-pdftools-jump-to-note)))
#+END_SRC

** Calibre ebook integration with org
#+BEGIN_SRC emacs-lisp
;; ──────────────────────────────────────────────────────────────
;; Calibre integration – only on laptop
;; ──────────────────────────────────────────────────────────────
(when (eq my-device 'laptop)
  (use-package calibredb
    :straight (:host github :repo "chenyanming/calibredb.el")
    :defer t
    :config
    (setq calibredb-root-dir "/wspace/src/calibre-ebooks")
    (setq calibredb-db-dir (expand-file-name "metadata.db" calibredb-root-dir))
    (setq calibredb-completion-method 'consult)
    (setq calibredb-search-limit 200)
    (setq calibredb-order '("title" "author" "tags"))
    (setq calibredb-virtual-library-alist '(("Engineering Books" . "tag:engineering OR tag:vlsi")
                                            ("Reference" . "tag:reference")))
    ;; Patch calibredb-get-cover to handle nil :book-dir gracefully
    (defun calibredb-get-cover (candidate &optional full-path)
      "Get cover image path of CANDIDATE.
If FULL-PATH, return the full path. Handles nil :book-dir."
      (let* ((id (calibredb-getattr candidate :id))
             (path (calibredb-getattr candidate :book-dir)))
        (when path  ; Skip if path is nil
          (let ((cover (expand-file-name "cover.jpg" path)))
            (if (file-exists-p cover)
                (if full-path
                    cover
                  (concat "file:" cover))
              nil)))))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
;; -----------------------------
;; Disable org-ref to avoid conflicts if installed
;; -----------------------------
(ignore-errors (unload-feature 'org-ref t))
(straight-override-recipe '(org-ref :type built-in :local-repo nil :files nil))
(setq load-path (cl-remove-if (lambda (p) (string-match-p "org-ref" p)) load-path))
(unless (featurep 'org-ref)
  (defvar org-ref-bibliography-notes nil)
  (defvar org-ref-default-bibliography nil)
  (defvar org-ref-pdf-directory nil)
  (provide 'org-ref))
#+END_SRC

** Magit is a Git GUI interface for Emacs
#+BEGIN_SRC emacs-lisp
(use-package magit
  :straight t
  :defer t
  :commands (magit-status magit-blame)
  :config
  (setq magit-display-buffer-function #'magit-display-buffer-fullframe-status-v1
        magit-save-repository-buffers 'dontask))
#+END_SRC

* ACTIVE UI & General Settings
#+BEGIN_SRC emacs-lisp
(when (eq my-device 'termux)
  (when (fboundp 'set-fringe-mode)
  (set-fringe-mode 0))
  (setq mouse-wheel-progressive-speed nil))
#+END_SRC

* ACTIVE General Settings

** Profiling
#+BEGIN_SRC emacs-lisp
(defvar my-config-el-start-time (current-time) "Time when config.el was started")
(setq my-config-el-start-time-iso (format-time-string "%Y-%m-%dT%T%:z"))
#+END_SRC

** UI Theme, word wrap and other settings.
#+BEGIN_SRC emacs-lisp
(load-theme 'tsdh-dark t)  ;;Dark theme for Emacs
(global-visual-line-mode 1) ;;Wrap text in GUI Windows
(when (fboundp 'set-fringe-mode)
  (set-fringe-mode 10)) ;;Sets the width of the left and right fringes (the empty margin space at the edge of windows in Emacs) to 10 pixels.
(setq-default cursor-type 'bar) ;;Changes the default cursor shape to a vertical bar (instead of the default box).
#+END_SRC


** Backups & autosaves
#+BEGIN_SRC emacs-lisp
(let ((backup-dir  (expand-file-name ".backups/" default-directory))
      (autosave-dir (expand-file-name ".autosaves/" default-directory)))
  (dolist (d (list backup-dir autosave-dir))
    (unless (file-directory-p d) (make-directory d t)))
  (setq backup-directory-alist `((".*" . ,backup-dir))
        auto-save-file-name-transforms `((".*" ,autosave-dir t))
        auto-save-default t
        version-control nil
        delete-old-versions t
        make-backup-files t
        backup-by-copying t))
#+END_SRC

** Miscellenous 
#+BEGIN_SRC emacs-lisp
;; Calendar: Monday as start of week
(setq-default calendar-week-start-day 1)

;; Sentences: No double space after periods
(setq-default sentence-end-double-space nil)

;; Truncate lines
(setq-default truncate-lines t)

;; Read-only files in view mode
(setq view-read-only t)

;; Added 2025-07-15: Allow alphabetical list continuation (1.a, 1.b, 1.c., ...)
(setq org-list-allow-alphabetical t)

;; Attachments relative to org file, in `.attach` folder
(setq org-attach-directory ".attach")

;; Enable smart window navigation
(windmove-default-keybindings) ;; Shift + Arrow
(winner-mode 1)                ;; C-c <left/right> undo/redo window layout

;; Timestamp functions
(defun my-insert-timestamp ()
  "Insert a timestamp in format YYYY-MM-DD HH:MM:SS"
  (interactive)
  (insert (format-time-string "%Y-%m-%d %H:%M:%S")))
(defun my-insert-datestamp ()
  "Insert a datestamp in format YYYY-MM-DD"
  (interactive)
  (insert (format-time-string "%Y-%m-%d")))
(defun my-insert-org-timestamp ()
  "Insert a timestamp in Org-mode format [YYYY-MM-DD Day HH:MM]"
  (interactive)
  (insert (format-time-string "[%Y-%m-%d %a %H:%M]")))
#+END_SRC

* ACTIVE Keybindings

** Org-mode specific keybinding for toggling items :Added 2025-07-15
#+BEGIN_SRC emacs-lisp
(add-hook 'org-mode-hook
          (lambda ()
            (local-set-key (kbd "C-c i i") #'org-toggle-item)))
#+END_SRC

** Switching windows 
#+BEGIN_SRC emacs-lisp
;; Ace Window for fast jumping (M-o) — key bound in org/keymaps.org
(use-package ace-window
  :straight t 
  :defer t)
#+END_SRC

** Tangle everything (C-c T)
#+BEGIN_SRC emacs-lisp
(defun my-tangle-all ()
  "Tangle all modular .org files including config.org."
  (interactive)
  (dolist (f '("config.org"
               "org/orgxtn.org" "org/roam.org" "org/filetags.org"
               "org/workflow.org" "org/notextn.org" "org/latextn.org"
               "org/engxtn.org" "org/mail.org" "org/keymaps.org"))
    (my/tangle-if-needed f (concat (file-name-sans-extension f) ".el")))
  (message "[%s] All files tangled" (format-time-string "%T")))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(dolist (pair '(("org/orgxtn.org"   . "org/orgxtn.el")
                ("org/roam.org"     . "org/roam.el")
                ("org/filetags.org" . "org/filetags.el")
                ("org/workflow.org" . "org/workflow.el")
                ("org/notextn.org"  . "org/notextn.el")
                ("org/latextn.org"  . "org/latextn.el")
                ("org/engxtn.org"   . "org/engxtn.el")
                ("org/mail.org"     . "org/mail.el")
                ("org/keymaps.org"  . "org/keymaps.el")))
  (let ((org (car pair)) (el (cdr pair)))
    (when (or (not (string-match-p "mail" org))
              (eq my-device 'laptop))
      (my/tangle-if-needed org el)
      (condition-case err
          (load (expand-file-name el user-emacs-directory) nil t)
        (error (message "Failed loading %s: %S" el err))))))

(provide 'config)
#+END_SRC


* Git sync for Org notes
Note: If you are not using git source control for org notes across devices, comment/remove following function

#+BEGIN_SRC emacs-lisp
;; Magit-based Git for Org notes sync
;; Assumes repo is on 'main' branch with 'origin' remote; customize as needed

;; Manual pull function (replaces git-pull-repo)
(defun magit-pull-org-repo ()
  "Pull the Org notes repo using Magit (async)."
  (interactive)
  (magit-pull-from-upstream nil) ; Pulls from origin/main; nil for default args
  (message "Magit pull started (check Magit process buffer if needed)."))

;; Manual push function (replaces git-push-repo)
(defun magit-push-org-repo ()
  "Stage modified files, commit if needed (with prompt), then push using Magit (async)."
  (interactive)
  (magit-stage-modified) ; Stages only tracked/modified files (safer than git add -A)
  (when (magit-anything-staged-p) ; Only commit if there are changes
    (magit-commit-create)) ; Interactive commit with message prompt
  (magit-push-current-to-upstream nil) ; Pushes to origin/main
  (message "Magit push started (check Magit process buffer if needed)."))

* CANCELLED Deprecated Settings
#+BEGIN_SRC emacs-lisp
;; Old timestamp code for Emacs < 27.1
;; (setq my-config-el-start-time-iso
;;       (concat (format-time-string "%Y-%m-%dT%T")
;;               ((lambda (x) (concat (substring x 0 3) ":" (substring x 3 5)))
;;                (format-time-string "%z"))))
#+END_SRC
