#+TITLE: Emacs Literate Config
#+TODO: ACTIVE | CANCELLED
#+STARTUP: indent
#+DATE: \today
#+FILETAGS: ::
#+OPTIONS: toc:t num:nil
#+PROPERTY: header-args :eval never-export
#+CREATED: 2025-07-12
#+LAST_MODIFIED: [2026-02-18 Wed 16:49]

* Purpose
This Org-mode configuration is the single source for my Emacs 30 setup, designed to be:
- **Future-proof** · Structured with comments, tables, CANCELLED blocks.
- **Reliable**     · Reproducible packages via straight.el, lazy loading.
- **Minimal**      · Single config.org, minimal hooks/keybindings.
- **Portable**     · Device-specific settings via device.el.
- **Synced**       · GitHub + Syncthing.
- **Modular**      · All extensions live in org/*.org files.

* My Digital Workflow :workflow:
| Purpose                         | Solution                              | Notes (device-specific in brackets)                              | Keybindings                          |
|---------------------------------|---------------------------------------|------------------------------------------------------------------|--------------------------------------|
| Package management              | use-package + straight.el             | Lazy loading, reproducible installs (all devices)                | —                                    |
| Filter Org headings             | sparse trees                          | Built-in Org (all devices)                                       | C-c /                                |
| Focus on subtree                | org-tree-to-indirect-buffer           | Built-in Org (all devices)                                       | C-c C-x b                            |
| Completion & search             | vertico + orderless                   | Fast, fuzzy, vertical UI (all devices)                           | —                                    |
| Buffer switching                | ibuffer                               | Better than default list (all devices)                           | C-x C-b                              |
| Insert timestamps               | custom functions                      | Various formats (all devices)                                    | C-c i t/d/o                          |
| Export to PDF                   | Org → LaTeX (AUCTeX)                  | pdf-tools for viewing (laptop preferred)                         | C-c C-e l p                          |
| Encrypt sections                | org-crypt                             | GPG-encrypted headings (all devices)                             | —                                    |
| Snippet management              | yankpad + yasnippet                   | Org-style snippets (all devices)                                 | C-c y / C-c Y                        |
| Spell checking                  | flyspell + Hunspell                   | UK English dictionary (all devices)                              | M-$                                  |
| PDF viewing & annotation        | pdf-tools + org-noter                 | Highlighting & synced notes (laptop preferred)                   | M-x org-noter                        |
| PDF side-by-side preview        | my/org-pdf-vertical-view              | Quick reference while writing (all devices)                      | C-c v                                |
| Zettelkasten / note-taking      | org-roam                              | Dailies, backlinks, UI on laptop                                 | C-c r n/f/i/d/T/P/N/D/t/r/s/g        |
| Bibliography & citations        | citar + org-roam integration          | Zotero sync, literature notes in roam (all devices)              | C-c i c/n/N/o/O                      |
| File-level tags                 | filetags                              | Vertico-powered tag completion (all devices)                     | C-c f t                              |
| Config tangling                 | org-babel-tangle + custom helpers     | Single or all files (all devices)                                | C-c t / C-c T                        |
| Quick capture                   | org-capture                           | Templates for notes/tasks (all devices)                          | C-c c                                |
| Toggle list items               | org-toggle-item                       | Convert text ↔ list (all devices)                                | C-c i i                              |
| Window navigation               | windmove + winner-mode + ace-window   | Move, undo/redo, jump (all devices)                              | Shift+Arrow / M-o                    |
| Execute code blocks             | Org Babel                             | Run shell/LaTeX/Python inline (all devices)                      | C-c C-c                              |
| Git sync for notes              | Magit                                 | Pull/push repo changes (all devices)                             | C-c g p / u                          |

* ACTIVE Core Setup

** Debug startup
#+BEGIN_SRC emacs-lisp
(when (getenv "MY_DEBUG_DEVICE")
  (message "=== STARTING CONFIG ==="))
#+END_SRC

** Garbage collection
#+BEGIN_SRC emacs-lisp
;; Fast startup: Temp raise GC, disable file handlers, tune process I/O
(defvar my--old-gc-cons-threshold gc-cons-threshold
  "Saved value of `gc-cons-threshold` to restore after init.")
(defvar my--old-file-name-handler-alist file-name-handler-alist
  "Saved value of `file-name-handler-alist` to restore after init.")

;; Make init fast
(setq gc-cons-threshold most-positive-fixnum   ; defer GC during init
      file-name-handler-alist nil
      read-process-output-max (* 1024 1024))   ; 1MB for fast subprocesses

(add-hook 'emacs-startup-hook
          (lambda ()
            (setq gc-cons-threshold (or my--old-gc-cons-threshold (* 16 1024 1024))
                  file-name-handler-alist my--old-file-name-handler-alist)))
#+END_SRC

** Disable startup screen.
#+BEGIN_SRC emacs-lisp
(setq inhibit-startup-screen t)
#+END_SRC

** Load device detection (with fallback)
#+BEGIN_SRC emacs-lisp
(condition-case err
    (load (expand-file-name "device.el" user-emacs-directory))
  (error (message "Failed to load device.el: %S" err)))

;; Fallback if device.el missing or failed
(unless (boundp 'my-device)
  (defvar my-device 'laptop)
  (message "my-device not set → defaulting to 'laptop"))

(message "Device: %s" my-device)
#+END_SRC

** Set default directory per device.
#+BEGIN_SRC emacs-lisp
(setq default-directory
      (cond ((eq my-device 'laptop) (expand-file-name "org/" "/wspace/"))
            ((eq my-device 'tablet) (expand-file-name "org/" (getenv "HOME")))
            ((eq my-device 'phone)  (expand-file-name "storage/notes" (getenv "HOME")))
            (t (expand-file-name "~/"))))

(unless (file-directory-p default-directory)
  (make-directory default-directory t))
(message "Default directory: %s" default-directory)

;; =========Stable global anchor for all path expansion ============
;; Captured here ONCE; never changes, even when buffer-local
;; default-directory shifts (e.g., opening files in books/)
(defvar my/notes-root-dir default-directory
  "Absolute path to notes root. Set once at startup. Device-portable.") 

#+END_SRC

** Global paths — defined very early

#+BEGIN_SRC emacs-lisp
;; Define paths early for global reuse (absolute/resolved for reliability)
(defvar bib-path (expand-file-name "bib/references.bib" default-directory))
(defvar books-bib-path (expand-file-name "bib/books-references.bib" default-directory))
(defvar my-citar-library-paths '("/wspace/src/zotero-kbase/storage"))
(defvar setupfile (expand-file-name "latex/setup-latex.org" default-directory))
(defvar org-roam-directory (expand-file-name "roam" default-directory))
(defvar org-roam-dailies-directory (expand-file-name "daily" org-roam-directory))
(defvar my-calibre-library-dir "/wspace/src/calibre-ebooks")
  
;; Create roam directories if missing
(dolist (d (list org-roam-directory org-roam-dailies-directory))
  (unless (file-directory-p d) (make-directory d t)))

(prefer-coding-system 'utf-8)
(setq default-buffer-file-coding-system 'utf-8)
#+END_SRC

** Tangling utilities
#+BEGIN_SRC emacs-lisp
;; modular org files tangle process.Tangling only happens when the .org file is newer than the .el file. 
(defun my-tangle-config-org ()
  "Tangle only config.org → config.el (C-c t)."
  (interactive)
  (condition-case err
      (progn
        (org-babel-tangle-file (expand-file-name "config.org" user-emacs-directory))
        (message "config.org tangled"))
    (error (message "Failed to tangle config.org: %S" err))))

(defun my/tangle-if-needed (org-file el-file)
  "Tangle ORG-FILE to EL-FILE only if ORG-FILE is newer."
  (let ((org-full (expand-file-name org-file user-emacs-directory))
        (el-full  (expand-file-name el-file  user-emacs-directory)))
    (when (or (not (file-exists-p el-full))
              (file-newer-than-file-p org-full el-full))
      (require 'org)
      (org-babel-tangle-file org-full el-full "emacs-lisp")
      (message "Tangled: %s → %s" org-file el-file))))
#+END_SRC

** Tag-based refiling
#+BEGIN_SRC emacs-lisp
;; automatically refiling (moving) Org headings based on their tags added on 2025-09-20
(defcustom my-org-refile-to-ids nil
  "Tag to target ID mappings."
  :group 'sacha
  :type '(repeat (cons string string)))
#+END_SRC

** Smart org-agenda-files (top-level + roam/)
#+BEGIN_SRC emacs-lisp
(defun my/update-agenda-files ()
  "Set `org-agenda-files` to top-level .org files in `default-directory`,
plus recursive .org files from the 'roam' subdirectory, excluding unwanted dirs."
  (interactive)
  (let* ((notes-dir (file-name-as-directory my/notes-root-dir))
         (roam-dir (expand-file-name "roam/" notes-dir))
         (excluded-dirs '("build" "ltximg" "images" ".attach" ".autosaves" ".backups" "bib" "latex" ".git"))
         (dir-predicate (lambda (d)
                          (let ((dirname (file-name-nondirectory d)))
                            (not (or (string-prefix-p "." dirname)
                                     (member dirname excluded-dirs))))))
         ;; Top-level .org files in root
         (top-level-files (when (file-directory-p notes-dir)
                            (directory-files notes-dir t "\\.org\\'")))
         (top-level-orgs (seq-filter (lambda (f)
                                       (and (file-regular-p f)
                                            (not (string-match-p "/\\.#" f))))
                                     top-level-files))
         ;; Recursive .org files in roam
         (roam-files (when (file-directory-p roam-dir)
                       (directory-files-recursively roam-dir "\\.org\\'" nil dir-predicate)))
         (roam-orgs (seq-filter (lambda (f)
                                  (and (file-regular-p f)
                                       (not (string-match-p "/\\.#" f))))
                                roam-files))
         (all-orgs (append top-level-orgs roam-orgs)))
    (if (not all-orgs)
        (progn
          (setq org-agenda-files nil)
          (message "my/update-agenda-files: no valid .org files found in %s or roam/" notes-dir))
      (setq org-agenda-files all-orgs)
      (message "org-agenda-files set (%d files) from %s and roam/" (length org-agenda-files) notes-dir))))

;; Initialize once
(my/update-agenda-files)

;; Rescan only when saving an Org file under the notes dir use stable var, not buffer-local default-directory
(add-hook 'after-save-hook
          (lambda ()
            (when (and (derived-mode-p 'org-mode)
                       buffer-file-name
                       (string-prefix-p (file-name-as-directory my/notes-root-dir)
                                        (file-name-directory (file-truename buffer-file-name))))
              (my/update-agenda-files))))
#+END_SRC


** Startup finished message
#+BEGIN_SRC emacs-lisp
(when (getenv "MY_DEBUG_DEVICE")
  (message "=== STARTUP COMPLETE ==="))
#+END_SRC

* ACTIVE Package Management
Configure package managers and lightweight, universal packages with lazy loading.
Heavy or laptop-specific packages are in org/workflows.org; org-roam and bibliographic tools in org/roam.org; filetags in org/filetags.org.

| Prefix / Key  | Command                               | Description                                          | Scope / Notes            |
| ------------- | ------------------------------------- | ---------------------------------------------------- | ------------------------ |
| C-c i(prefix) | —                                     | Citations, literature notes, timestamps, PDF tools   | Main bib & note workflow |
| C-c i c       | citar-insert-citation                 | Insert citation @key                                 | Org-mode                 |
| C-c i C       | citar-insert-citation                 | Same as above (Shift for muscle memory)              | Org-mode                 |
| C-c i r       | citar-insert-reference                | Insert formatted reference                           | Org-mode                 |
| C-c i k       | citar-insert-keys                     | Insert bare citation keys                            | Org-mode                 |
| C-c i n       | citar-create-note                     | Create new blank literature note                     | Any mode                 |
| C-c i N       | citar-open-note                       | Open/create note for citation at point               | Any mode                 |
| C-c i C-n     | citar-open-notes                      | Choose any entry → open its note                     | Any mode                 |
| C-c i o       | citar-open                            | Open PDF/link/website                                | Any mode                 |
| C-c i O       | citar-open-files                      | Open all attached files                              | Any mode                 |
| C-c i a       | my/open-pdf-and-note                  | Open PDF + start org-noter session                   | Any mode                 |
| C-c i g       | my/open-pdf-and-note 'generic         | Same but generic (no special handling)               | Any mode                 |
| C-c i p       | my/pdf-insert-selection-to-note       | Insert selected PDF text into note                   | pdf-view-mode            |
| C-c i A       | org-noter-pdftools-insert-annotations | Insert PDF annotations into note                     | pdf-view-mode            |
| C-c i b       | my/open-calibre-book-and-note         | Open Calibre book + note (laptop only)               | Laptop only              |
| C-c i t       | my-insert-timestamp                   | Insert YYYY-MM-DD HH:MM:SS                           | Any mode                 |
| C-c i d       | my-insert-datestamp                   | Insert YYYY-MM-DD                                    | Any mode                 |
| C-c i o       | my-insert-org-timestamp               | Insert Org timestamp [YYYY-MM-DD Day HH:MM]          | Any mode                 |
| C-c r(prefix) | —                                     | Pure Org-roam navigation & dailies                   | —                        |
| C-c r f       | org-roam-node-find                    | Find node                                            | Org-mode                 |
| C-c r i       | org-roam-node-insert                  | Insert node link                                     | Org-mode                 |
| C-c r n       | org-roam-capture                      | Capture new node                                     | Org-mode                 |
| C-c r d       | org-roam-dailies-capture-today        | Capture today’s daily                                | Org-mode                 |
| C-c r T       | org-roam-dailies-goto-today           | Go to today’s daily                                  | Org-mode                 |
| C-c r P       | org-roam-dailies-goto-previous-note   | Previous daily                                       | Org-mode                 |
| C-c r N       | org-roam-dailies-goto-next-note       | Next daily                                           | Org-mode                 |
| C-c r D       | org-roam-dailies-goto-date            | Go to specific date                                  | Org-mode                 |
| C-c r t       | my-org-roam-tag-add                   | Add tag to node                                      | Org-mode                 |
| C-c r r       | my-org-roam-tag-remove                | Remove tag from node                                 | Org-mode                 |
| C-c r s       | my/org-roam-safe-rebuild              | Safe DB rebuild                                      | Org-mode                 |
| C-c r g       | org-roam-ui-open                      | Open graph UI (laptop only)                          | Laptop only              |
| C-c g(prefix) | —                                     | Git sync for Org notes                               | —                        |
| C-c g p       | magit-pull-org-repo                   | Pull changes from repo                               | Any mode                 |
| C-c g u       | magit-push-org-repo                   | Push changes to repo                                 | Any mode                 |
| Consult keys  | —                                     | Global keys for enhanced completion and navigation   | Global                   |
| C-s           | consult-line                          | Search lines                                         | Global (overridden in pdf-view-mode)                   |
| C-c h         | consult-org-heading                   | Search org headings                                  | Global                   |
| C-c k         | consult-ripgrep                       | Search with ripgrep                                  | Global                   |
| C-c b         | consult-buffer                        | Switch buffer                                        | Global                   |
| C-c p         | consult-find                          | Find files                                           | Global                   |
| C-s (PDF)     | pdf-isearch-forward                   | Incremental text search in current PDF               | pdf-view-mode only       |
| C-r (PDF)     | pdf-isearch-backward                  | Reverse incremental search in current PDF            | pdf-view-mode only       |
| C-c t         | my-tangle-config-org                  | Tangle only config.org                               | Any mode                 |
| C-c T         | my-tangle-all                         | Tangle all modular files                             | Any mode                 |
| C-c c         | org-capture                           | Quick capture                                        | Any mode                 |
| C-c a         | org-agenda                            | Agenda view                                          | Any mode                 |
| C-c v         | my/org-pdf-vertical-view              | Side-by-side PDF preview                             | Any mode                 |
| C-c f t       | my-org-set-filetags                   | Set file-level tags                                  | Org-mode                 |
| C-x g         | magit-status                          | Full Magit status                                    | Any mode                 |
| C-c m         | mu4e                                  | Open email (Mu4e)                                    | Any mode                 |
| C-z           | undo                                  | Undo                                                 | Any mode                 |
| M-o           | ace-window                            | Fast window switching                                | Any mode                 |
| TAB (Org)     | my/org-smart-tab                      | Smart TAB (yasnippet / CDLaTeX / visibility cycling) | Org-mode                 |
| C-c i i (Org) | org-toggle-item                       | Toggle list item                                     | Org-mode                 |

** Org Base Configuration
#+BEGIN_SRC emacs-lisp
(use-package org
  :straight (:type built-in)
  :ensure nil
  :config

  ;; Minimal fix: preserve indentation in source blocks (prevents comma escaping)
  (setq org-src-preserve-indentation t)
  
  ;; Core requires
  (require 'oc)
  (require 'oc-biblatex)
  
 ;; Org-cite export processors: use biblatex for LaTeX
 ;; (setq org-cite-export-processors
 ;;      '((latex biblatex)
 ;;        (html csl)
 ;;        (t csl)))

  ;; Enable refiling to any heading in agenda files (incl. roam/notes/mobile)
  (setq org-refile-targets '((org-agenda-files :maxlevel . 5)))
  (setq org-refile-use-outline-path 'file) ;; Show file paths in prompt
  (setq org-outline-path-complete-in-steps nil) ;; Complete in one step (Vertico fuzzy)
  (setq org-refile-allow-creating-parent-nodes 'confirm)
  (setq org-refile-use-cache t))  ;; Cache targets for speed
#+END_SRC

** Ensure use-package is available.
#+BEGIN_SRC emacs-lisp
(eval-when-compile
  (require 'use-package))
#+END_SRC

** File manipulation library (loaded on demand).
#+BEGIN_SRC emacs-lisp
(use-package f
  :straight t
  :defer t)
#+END_SRC

** Hash table utilities (loaded on demand).
#+BEGIN_SRC emacs-lisp
(use-package ht
  :straight t
  :defer t)
#+END_SRC

** Ibuffer for buffer management (loaded on C-x C-b).
#+BEGIN_SRC emacs-lisp
(use-package ibuffer
  :straight t
  :defer t
  :bind ("C-x C-b" . ibuffer))
#+END_SRC

** Encrypt org files
#+BEGIN_SRC emacs-lisp
(use-package org-crypt
  :ensure nil                        ;; Do not install from ELPA
  :straight nil                      ;; Do not use straight.el
  :defer t                           ;; Load when needed (on demand)
  :config
  (setq org-crypt-use-before-save nil))  ;; Optional: prevent auto-encryption on save
#+END_SRC

** Flyspell for spell checking (loaded on M-$).
#+BEGIN_SRC emacs-lisp
(use-package flyspell
  :defer t
  :bind ("M-$" . flyspell-correct-word-before-point)
  :init
  ;; Force Hunspell with UK English dictionary
  (setq ispell-program-name "hunspell"
        ispell-dictionary "en_GB"
        ispell-local-dictionary "en_GB"
        ispell-local-dictionary-alist
        '(("en_GB" "[[:alpha:]]" "[^[:alpha:]]" "[']" t
           ("-d" "en_GB") nil utf-8)))
  (unless (executable-find "hunspell") (message "Hunspell not found; spellcheck disabled."))        
  :config
  ;; Enable flyspell everywhere you write text
  (add-hook 'text-mode-hook #'flyspell-mode)
  ;; For code: only check strings & comments
  (add-hook 'prog-mode-hook #'flyspell-prog-mode)

  ;; Correction interface for M-$
  (use-package flyspell-correct :straight t))

#+END_SRC

** CD Latex package for latex equations
#+BEGIN_SRC emacs-lisp
 (use-package cdlatex 
  :straight t 
  :defer t
  :hook (org-mode . org-cdlatex-mode))
#+END_SRC

#+BEGIN_SRC emacs-lisp
;; ──────────────────────────────────────────────────────────────
;; math-delimiters: Smart \( \) / \[ \] insertion (best companion to cdlatex)
;; ──────────────────────────────────────────────────────────────
(use-package math-delimiters
  :straight (math-delimiters :type git :host github :repo "oantolin/math-delimiters")
  :after (org cdlatex)               ; Load after these to be safe
  :config
  ;; Most popular & ergonomic choice: bind to $ (like everyone else does)
  (with-eval-after-load 'org
    (define-key org-mode-map (kbd "$") #'math-delimiters-insert))

  ;; Very important: prevent conflict with CDLaTeX's own $ binding
  (with-eval-after-load 'cdlatex
    (define-key cdlatex-mode-map (kbd "$") nil))

  ;; Optional extras you might want later:
  ;; - Bind also in pure LaTeX buffers (if you ever edit .tex files directly)
  ;; (with-eval-after-load 'latex
  ;;   (define-key LaTeX-mode-map (kbd "$") #'math-delimiters-insert))

  ;; - If you prefer triggering on typing \( or \[ instead:
  ;; (define-key org-mode-map (kbd "\\(") #'math-delimiters-insert)
  ;; (define-key org-mode-map (kbd "\\[") #'math-delimiters-insert)
  )
 #+END_SRC 
   
** AUCTeX is an extensible package for writing and formatting TeX files in Emacs and XEmacs
#+BEGIN_SRC emacs-lisp
(use-package auctex
  :straight t
  :defer t  ;; Load on demand
  :hook (LaTeX-mode . (lambda () (turn-on-reftex) (flyspell-mode)))  ;; Optional: RefTeX for refs, spell-check
  :config
  (setq TeX-auto-save t
        TeX-parse-self t))
#+END_SRC

** Yasnippet package for adding snippets in org files
#+BEGIN_SRC emacs-lisp
(use-package yasnippet
  :straight t
  :bind ("C-c s" . yas-insert-snippet)  ;; Pop up selectable snippets (e.g., tbl, fig)
  :config
  (yas-global-mode 1)
  (setq yas-indent-line 'fixed)  ;; Preserves indentation
  (add-to-list 'yas-snippet-dirs (expand-file-name "snippets" user-emacs-directory))
  (yas-reload-all)
  (add-hook 'org-mode-hook #'yas-minor-mode)
  :diminish yas-minor-mode)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package org-download
  :straight t
  :after org
  :config
  ;; Configure org-download to integrate with org-attach
  (setq org-download-method 'attach            ; Use org-attach directory
        org-download-image-dir ".attach/images" ; Subdirectory for organization
        org-download-screenshot-method "scrot -s %s") ; Use scrot for screenshots

  ;; Bind screenshot shortcut only if scrot is available
  (when (executable-find "scrot")
    (bind-key "C-c C-x C-s" #'org-download-screenshot))

  ;; Enable org-download in org-mode
  (add-hook 'org-mode-hook #'org-download-enable))
#+END_SRC

** Yankpad: Org-mode snippet library on top of Yasnippet
#+BEGIN_SRC emacs-lisp
(use-package yankpad
  :straight t
  :bind
  (("C-c Y" . yankpad-expand)  ;; Expand snippet at point with yasnippet evaluation
   ("C-c y" . yankpad-insert)) ;; Insert snippet as plain text (no evaluation)
  :config
  ;; Set yankpad file in a portable location
  (setq yankpad-file (expand-file-name "org/yankpad.org"
                                       user-emacs-directory))
  ;; Ensure directory exists
  (let ((yankpad-dir (file-name-directory yankpad-file)))
    (unless (file-directory-p yankpad-dir)
      (make-directory yankpad-dir t)))

  ;; Optional: auto-reload in Org buffers
  (add-hook 'org-mode-hook #'yankpad-reload))
#+END_SRC

** Vertico: vertical completion UI Work well with org-roam (and Emacs in general) much faster, more flexible, and user-friendly. 
#+BEGIN_SRC emacs-lisp
(use-package vertico
  :straight t
  :init (vertico-mode))
#+END_SRC

** Orderless: smart fuzzy matching for completion
#+BEGIN_SRC emacs-lisp
(use-package orderless
  :straight t
  :custom
  (completion-styles '(orderless))
  (completion-category-defaults nil)
  (completion-category-overrides '((file (styles partial-completion)))))
#+END_SRC

** Future enhancement Consult package: enhanced commands. :future:
Powerful, fast,and flexible search/navigation UI (search, buffer switch, etc.) for working with Org-roam and Emacs in general.It can be enabled later for future optimization.

** bibtex-completion for bibtex citar implementation
#+BEGIN_SRC emacs-lisp
;; -----------------------------
;; Marginalia — Rich Annotations (metadata for minibuffer)
;; -----------------------------
(use-package marginalia
  :straight t
  :after vertico
  :init (marginalia-mode)
  :config
  (setq marginalia-align 'right)
  (message "✅ Marginalia annotations active."))
#+END_SRC

#+BEGIN_SRC emacs-lisp

;; Suppress embark-consult false-positive timing warning
(defun my/suppress-embark-consult-warning (type msg &rest _)
  (not (and (eq type 'emacs)
            (string-match-p "embark-consult" msg))))
(advice-add 'display-warning :before-while
            #'my/suppress-embark-consult-warning)


;; -----------------------------
;; Embark — Contextual Actions
;; -----------------------------
(use-package embark
  :straight t
  :after vertico
  :demand t  ; Load immediately after Vertico to resolve deferral for downstream packages like Consult
  :bind (("C-." . embark-act)
         ("C-;" . embark-dwim))
  :config
  (setq prefix-help-command #'embark-prefix-help-command))
#+END_SRC

#+BEGIN_SRC emacs-lisp 
(use-package consult
  :straight t
  :after (vertico embark))  ; Defer config/bindings until dependencies are read
#+END_SRC


#+BEGIN_SRC emacs-lisp
;; Integrate Embark + Consult (if you have consult; otherwise omit)
(use-package embark-consult
  :straight (:host github
             :repo "oantolin/embark"
             :files ("embark-consult.el"))  ;;just this one file, same repo
  :after embark
  :demand t
  :hook (embark-collect-mode . consult-preview-at-point-mode)
  :config (message "✅ Embark-Consult integration enabled."))
#+END_SRC

** RefTeX for Advanced Citation/Ref Handling.
If using AUCTeX for LaTeX exports, add reftex package.

#+BEGIN_SRC emacs-lisp
(use-package reftex
  :straight t
  :defer t
  :diminish reftex-mode)
#+END_SRC

** pdf tools: Enable in-buffer PDF viewing in Emacs (rather than opening PDFs in external viewers)

#+BEGIN_SRC emacs-lisp
;; Fix the pdf-cache-clear-data Call
(defun my/pdf-clear-cache-safely ()
  "Clear PDF cache without arguments, only if buffer is a PDF."
  (when (and buffer-file-name (string-match-p "\\.pdf\\'" buffer-file-name))
    (pdf-cache-clear-data))) ; No argument!


;; Use features like highlighting, annotations, or text selection. Work along with org-noter for taking notes synchronized with PDF pages
;; Updated on 2025-11-26 to fix epdfinfo executable error after Poppler upgrade.
;; ──────────────────────────────────────────────────────────────
;; PDF Tools + Org-Noter-PDFTools – FINAL WORKING VERSION (2025-11-26)
;; ──────────────────────────────────────────────────────────────
;; Org-Noter + PDF-Tools for Note Annotation
(when (eq my-device 'laptop) ; Skip heavy PDF on mobile
  (use-package pdf-tools
    :straight (:host github :repo "vedang/pdf-tools" :build t)
    :demand t ; Load early to avoid session failures
    :mode ("\\.pdf\\'" . pdf-view-mode)
    :magic ("%PDF" . pdf-view-mode)
    :config
    (setq pdf-info-epdfinfo-program
          (expand-file-name "build/server/epdfinfo" (straight--build-dir "pdf-tools")))
    ;; Automatically revert unmodified PDF buffers
    (setq revert-without-query '(".+\\.pdf$"))
    (condition-case nil
        (unless (file-executable-p pdf-info-epdfinfo-program)
          (pdf-tools-install :no-query))
      (error (message "PDF-Tools install failed; check Poppler deps")))
    (setq pdf-view-display-size 'fit-page
          pdf-view-continuous t
          pdf-annot-activate-created-annotations nil
          pdf-tools-handle-upgrades nil
          pdf-view-use-scaling t)
  ;; load pdf-isearch and activate isearch mode
    (require 'pdf-isearch)
    (add-hook 'pdf-view-mode-hook #'pdf-isearch-minor-mode)
    (add-hook 'pdf-view-mode-hook #'my/pdf-clear-cache-safely)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
;; ──────────────────────────────────────────────────────────────
;; Custom: Auto-Sync Dual-Page Navigation (No external dependencies)
;; ──────────────────────────────────────────────────────────────
(defvar my/pdf-dual-sync-enabled nil
  "Non-nil when dual-page sync is active.")

(defun my/pdf-sync-advice (orig-fun &rest args)
  "Advice to sync dual windows when navigating."
  (if (not my/pdf-dual-sync-enabled)
      (apply orig-fun args)  ;; Normal single-window behavior
    (let* ((wins (get-buffer-window-list (current-buffer)))
           (current-win (selected-window))
           (other-win (car (delq current-win wins))))
      ;; Execute in current window
      (apply orig-fun args)
      ;; Sync to other window if it exists
      (when (and other-win (window-live-p other-win))
        (with-selected-window other-win
          (apply orig-fun args))))))

(defun my/pdf-view-dual-mode ()
  "Toggle side-by-side two-page view WITH auto-sync navigation."
  (interactive)
  (if (> (length (get-buffer-window-list (current-buffer))) 1)
      ;; Disable: close extra windows and turn off sync
      (progn
        (delete-other-windows)
        (setq my/pdf-dual-sync-enabled nil)
        (message "Dual-page sync: OFF"))
    ;; Enable: split window and turn on sync
    (let ((buf (current-buffer))
          (page (pdf-view-current-page)))
      (delete-other-windows)
      (split-window-right)
      (other-window 1)
      (set-window-buffer nil buf)
      (pdf-view-goto-page (1+ page))
      (pdf-view-fit-page-to-window)
      (other-window -1)
      (pdf-view-fit-page-to-window)
      (setq my/pdf-dual-sync-enabled t)
      (message "Dual-page sync: ON (use n/p/SPC/DEL)"))))

;; Apply advice to pdf-tools navigation commands
(with-eval-after-load 'pdf-view
  (advice-add 'pdf-view-next-page :around #'my/pdf-sync-advice)
  (advice-add 'pdf-view-previous-page :around #'my/pdf-sync-advice)
  (advice-add 'pdf-view-scroll-up-or-next-page :around #'my/pdf-sync-advice)
  (advice-add 'pdf-view-scroll-down-or-previous-page :around #'my/pdf-sync-advice)
  (define-key pdf-view-mode-map (kbd "d") #'my/pdf-view-dual-mode))
#+END_SRC

** pdf annotation org noter optional dependancies
#+BEGIN_SRC emacs-lisp
;; Optional for org-noter EPUB/DJVU (suppress warnings if not used)
(use-package nov
  :straight t
  :defer t)

(use-package djvu
  :straight t
  :defer t)
#+END_SRC

#+BEGIN_SRC emacs-lisp
;; -----------------------------
;; Disable org-ref to avoid conflicts if installed
;; -----------------------------
(ignore-errors (unload-feature 'org-ref t))
(straight-override-recipe '(org-ref :type built-in :local-repo nil :files nil))
(setq load-path (cl-remove-if (lambda (p) (string-match-p "org-ref" p)) load-path))
(unless (featurep 'org-ref)
  (defvar org-ref-bibliography-notes nil)
  (defvar org-ref-default-bibliography nil)
  (defvar org-ref-pdf-directory nil)
  (provide 'org-ref))
#+END_SRC

** Zotero/Bibliography Integration
#+BEGIN_SRC emacs-lisp
;; -----------------------------
;; Citar Package Configuration
;; -----------------------------
(use-package citar
  :straight t
  :no-require t
  :commands (citar-insert-citation citar-create-note)
  :custom
  (org-cite-global-bibliography (list bib-path books-bib-path))
  (citar-bibliography org-cite-global-bibliography)
  (citar-library-paths my-citar-library-paths)
  (citar-notes-paths (list (expand-file-name "literature" org-roam-directory)))
  (citar-file-note-extensions '("org"))
  (org-cite-insert-processor 'citar)
  (org-cite-follow-processor 'citar)
  (org-cite-activate-processor 'citar)
  (citar-file-open-function 'find-file)
  :hook
  ((org-mode . citar-capf-setup)
   (LaTeX-mode . citar-capf-setup))
  :config
  (require 'citar)  ; Load full package
  (message "✅ Citar ready."))

;; Install C-c i c in org buffers only (safe)
(add-hook 'org-mode-hook
          (lambda ()
            ;; prefer local-set-key to avoid overriding global maps
            (local-set-key (kbd "C-c i c") #'citar-insert-citation)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
;; Helper function to insert or create citation notes
(defun my/citar-insert-or-create-note ()
  "Insert citation if in Org-mode; otherwise, create bib note."
  (interactive)
  (if (derived-mode-p 'org-mode)
      (call-interactively 'citar-insert-citation)
    (call-interactively 'citar-create-note)))
#+END_SRC

** Org noter + Citar annotation: added 2025-11-02
#+BEGIN_SRC emacs-lisp
(use-package org-noter
  :straight t
  :after (org pdf-tools)
  :demand t  ; Ensure ready for bindings
  :config
  (setq org-noter-notes-search-path (list (expand-file-name "literature" org-roam-directory))
        org-noter-default-notes-file-names '("notes.org" "annotations.org")
        org-noter-auto-save-last-location nil
        org-noter-highlight-selected-text nil
        org-noter-insert-note-no-questions t
        org-noter-hide-other nil
        org-noter-doc-property-in-notes nil))
#+END_SRC

** pdf annotation using org noter
#+BEGIN_SRC emacs-lisp
;; ──────────────────────────────────────────────────────────────
;; Org-pdftools (includes org-noter-pdftools integration)
;; ──────────────────────────────────────────────────────────────
(when (eq my-device 'laptop)
  (use-package org-pdftools
    :straight (org-pdftools :type git :host github :repo "fuxialexander/org-pdftools"
                            :files ("*.el"))  ; Includes both org-pdftools.el and org-noter-pdftools.el
    :after (org-noter pdf-tools)
    :demand t
    :config
    ;; Require the bundled org-noter-pdftools
    (require 'org-noter-pdftools)
    (org-pdftools-setup-link) 
    (with-eval-after-load 'pdf-view
    ;; Shortcut: Press 'v' in PDF to jump to Org note
    (define-key pdf-view-mode-map (kbd "v") #'org-noter-pdftools-jump-to-note))))
#+END_SRC

**  Content inclusion from other Org Mode file
#+BEGIN_SRC emacs-lisp
;; Define and localize the indent variable once
(defvar org-transclusion-indent-mode nil
  "Non-nil means org-transclusion indents transcluded contents.")

(with-eval-after-load 'org-transclusion
  (make-variable-buffer-local 'org-transclusion-indent-mode))

(use-package org-transclusion
  :straight t
  :after org
  :config
  ;;(add-hook 'org-mode-hook #'org-transclusion-mode)
  ;; Optional: actually enable indentation in Org buffers
  (add-hook 'org-mode-hook
             (lambda ()
               (setq-local org-transclusion-indent-mode t)))
  )

#+END_SRC

** Calibre ebook integration with org
#+BEGIN_SRC emacs-lisp
;; ──────────────────────────────────────────────────────────────
;; Calibre integration – only on laptop
;; ──────────────────────────────────────────────────────────────
(when (eq my-device 'laptop)
  (use-package calibredb
    :straight (:host github :repo "chenyanming/calibredb.el")
    :defer t
    :config
    ;; Core calibredb paths
    (setq calibredb-root-dir "/wspace/src/calibre-ebooks")
    (setq calibredb-db-dir (expand-file-name "metadata.db" calibredb-root-dir))

    ;; UI/behaviour
    (setq calibredb-completion-method 'consult
          calibredb-search-limit      200
          calibredb-order             '("title" "author" "tags")
          calibredb-virtual-library-alist
          '(("Engineering Books" . "tag:engineering OR tag:vlsi")
            ("Reference"         . "tag:reference")))

    ;; Robust cover lookup (handle nil :book-dir)
    (defun calibredb-get-cover (candidate &optional full-path)
      "Get cover image path of CANDIDATE.
If FULL-PATH, return the full path. Handles nil :book-dir."
      (let* ((path (calibredb-getattr candidate :book-dir)))
        (when path
          (let ((cover (expand-file-name "cover.jpg" path)))
            (when (file-exists-p cover)
              (if full-path
                  cover
                (concat "file:" cover)))))))

    ;; ------------------------------------------------------------------
    ;; Export books BibTeX catalog for Org/Citar (uses relative org paths)
    ;; ------------------------------------------------------------------
    (defun my/calibredb-export-books-bib ()
      "Regenerate `books-bib-path` from the Calibre library (laptop only)."
      (interactive)
      (let* ((bib-file (expand-file-name books-bib-path))
             (lib-dir  (expand-file-name calibredb-root-dir))
             (cmd (format
                   "calibredb catalog \"%s\" --with-library \"%s\" --fields %s"
                   bib-file
                   lib-dir
                   "title,authors,author_sort,publisher,pubdate,isbn,id")))
        (message "Running: %s" cmd)
        (let ((exit-code (shell-command cmd "*calibredb-bib*" "*calibredb-bib*")))
          (if (zerop exit-code)
              (message "Updated %s" bib-file)
            (message "calibredb failed; see *calibredb-bib* buffer")))))))
#+END_SRC

** Hugo website/blog generator
#+BEGIN_SRC emacs-lisp
;; Exports an org-mode file to multiple markdown files that are then used 
;; by the hugo static site generator to generator the website

(use-package ox-hugo
  :straight t
  :after ox)  ; Load after Org export
#+END_SRC

** Magit is a Git GUI interface for Emacs
#+BEGIN_SRC emacs-lisp
(use-package magit
  :straight t
  :defer t
  :commands (magit-status magit-blame)
  :config
  (setq magit-display-buffer-function #'magit-display-buffer-fullframe-status-v1
        magit-save-repository-buffers 'dontask))
#+END_SRC

* ACTIVE UI & General Settings
#+BEGIN_SRC emacs-lisp
(when (eq my-device 'termux)
  (when (fboundp 'set-fringe-mode)
  (set-fringe-mode 0))
  (setq mouse-wheel-progressive-speed nil))
#+END_SRC

** Glossary package for Acronmys                               :enhancement:
#+BEGIN_SRC emacs-lisp
;; ========================================
;; Org-glossary package
;; Added: 2026-02-18
;; ========================================
(use-package org-glossary
  :straight (org-glossary :type git :host github :repo "tecosaur/org-glossary")
  :after org
  :commands (org-glossary-mode))
  
;; Fix: org-glossary infinite recursion in dependency watcher
(defvar my/org-glossary--visiting nil
  "Files currently being visited by register-buffer-dependencies.")

(with-eval-after-load 'org-glossary
  (advice-add 'org-glossary--register-buffer-dependencies :around
              (lambda (orig &optional path-spec)
                (let* ((path-spec (or path-spec
                                      (org-glossary--complete-path-spec path-spec)))
                       (file (plist-get path-spec :file)))
                  (if (and file (member (expand-file-name file)
                                        my/org-glossary--visiting))
                      nil  ; already visiting — break cycle
                    (let ((my/org-glossary--visiting
                           (if file
                               (cons (expand-file-name file)
                                     my/org-glossary--visiting)
                             my/org-glossary--visiting)))
                      (funcall orig path-spec)))))))
#+END_SRC

* ACTIVE General Settings

** Profiling
#+BEGIN_SRC emacs-lisp
(defvar my-config-el-start-time (current-time) "Time when config.el was started")
(setq my-config-el-start-time-iso (format-time-string "%Y-%m-%dT%T%:z"))
#+END_SRC

** UI Theme, word wrap and other settings.
#+BEGIN_SRC emacs-lisp
(load-theme 'tango-dark t)  ;;Dark theme for Emacs
(global-visual-line-mode 1) ;;Wrap text in GUI Windows
(when (fboundp 'set-fringe-mode)
  (set-fringe-mode 10)) ;;Sets the width of the left and right fringes (the empty margin space at the edge of windows in Emacs) to 10 pixels.
(setq-default cursor-type 'bar) ;;Changes the default cursor shape to a vertical bar (instead of the default box).
#+END_SRC


** Backups & autosaves
#+BEGIN_SRC emacs-lisp
(let ((backup-dir  (expand-file-name ".backups/" default-directory))
      (autosave-dir (expand-file-name ".autosaves/" default-directory)))
  (dolist (d (list backup-dir autosave-dir))
    (unless (file-directory-p d) (make-directory d t)))
  (setq backup-directory-alist `((".*" . ,backup-dir))
        auto-save-file-name-transforms `((".*" ,autosave-dir t))
        auto-save-default t
        version-control nil
        delete-old-versions t
        make-backup-files t
        backup-by-copying t))
#+END_SRC

** Miscellenous 
#+BEGIN_SRC emacs-lisp
;; Calendar: Monday as start of week
(setq-default calendar-week-start-day 1)

;; Sentences: No double space after periods
(setq-default sentence-end-double-space nil)

;; Truncate lines
(setq-default truncate-lines t)

;; Read-only files in view mode
(setq view-read-only t)

;; Added 2025-07-15: Allow alphabetical list continuation (1.a, 1.b, 1.c., ...)
(setq org-list-allow-alphabetical t)

;; Attachments relative to org file, in `.attach` folder
(setq org-attach-directory ".attach")

;; Enable smart window navigation
(windmove-default-keybindings) ;; Shift + Arrow
(winner-mode 1)                ;; C-c <left/right> undo/redo window layout

;; Enable smart quotes while exporting to pdf
(setq org-export-with-smart-quotes t)

;; Timestamp functions
(defun my-insert-timestamp ()
  "Insert a timestamp in format YYYY-MM-DD HH:MM:SS"
  (interactive)
  (insert (format-time-string "%Y-%m-%d %H:%M:%S")))
(defun my-insert-datestamp ()
  "Insert a datestamp in format YYYY-MM-DD"
  (interactive)
  (insert (format-time-string "%Y-%m-%d")))
(defun my-insert-org-timestamp ()
  "Insert a timestamp in Org-mode format [YYYY-MM-DD Day HH:MM]"
  (interactive)
  (insert (format-time-string "[%Y-%m-%d %a %H:%M]")))
#+END_SRC

* ACTIVE Keybindings

** Org-mode specific keybinding for toggling items :Added 2025-07-15
#+BEGIN_SRC emacs-lisp
(add-hook 'org-mode-hook
          (lambda ()
            (local-set-key (kbd "C-c i i") #'org-toggle-item)))
#+END_SRC

** Switching windows 
#+BEGIN_SRC emacs-lisp
;; Ace Window for fast jumping (M-o) — key bound in org/keymaps.org
(use-package ace-window
  :straight t 
  :defer t)
#+END_SRC

** Tangle everything (C-c T)
#+BEGIN_SRC emacs-lisp
(defun my-tangle-all ()
  "Tangle all modular .org files including config.org."
  (interactive)
  (dolist (f '("config.org"
               "org/orgxtn.org" "org/roam.org" "org/filetags.org"
               "org/workflow.org" "org/notextn.org" "org/latextn.org"
               "org/engxtn.org" "org/mail.org" "org/keymaps.org"))
    (my/tangle-if-needed f (concat (file-name-sans-extension f) ".el")))
  (message "[%s] All files tangled" (format-time-string "%T")))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(dolist (pair '(("org/orgxtn.org"   . "org/orgxtn.el")
                ("org/roam.org"     . "org/roam.el")
                ("org/filetags.org" . "org/filetags.el")
                ("org/workflow.org" . "org/workflow.el")
                ("org/notextn.org"  . "org/notextn.el")
                ("org/latextn.org"  . "org/latextn.el")
                ("org/engxtn.org"   . "org/engxtn.el")
                ("org/mail.org"     . "org/mail.el")
                ("org/keymaps.org"  . "org/keymaps.el")))
  (let ((org (car pair)) (el (cdr pair)))
    (when (or (not (string-match-p "mail" org))
              (eq my-device 'laptop))
      (my/tangle-if-needed org el)
      (condition-case err
          (load (expand-file-name el user-emacs-directory) nil t)
        (error (message "Failed loading %s: %S" el err))))))

(provide 'config)
#+END_SRC

* Git sync for Org notes
Note: If you are not using git source control for org notes across devices, comment/remove following function

#+BEGIN_SRC emacs-lisp
;; Magit-based Git for Org notes sync
;; Assumes repo is on 'main' branch with 'origin' remote; customize as needed

;; Manual pull function (replaces git-pull-repo)
(defun magit-pull-org-repo ()
  "Pull the Org notes repo using Magit (async)."
  (interactive)
  (if (fboundp 'magit-pull-from-upstream)
      (magit-pull-from-upstream nil)
    (magit-pull-current-branch))  ; Fallback to stable API
  (message "Magit pull started."))

;; Manual push function (replaces git-push-repo)
(defun magit-push-org-repo ()
  "Stage modified files, commit if needed (with prompt), then push using Magit (async)."
  (interactive)
  (magit-stage-modified)
  (when (and (fboundp 'magit-anything-staged-p) (magit-anything-staged-p))
    (magit-commit-create))
  (if (fboundp 'magit-push-current-to-upstream)
      (magit-push-current-to-upstream nil)
    (magit-push-current-branch))  ; Fallback
  (message "Magit push started."))

* CANCELLED Deprecated Settings
#+BEGIN_SRC emacs-lisp
;; Old timestamp code for Emacs < 27.1
;; (setq my-config-el-start-time-iso
;;       (concat (format-time-string "%Y-%m-%dT%T")
;;               ((lambda (x) (concat (substring x 0 3) ":" (substring x 3 5)))
;;                (format-time-string "%z"))))
#+END_SRC
